plugins {
    id 'kotlin-kapt'
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
}

Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

android {
    assetPacks = [":fast_follow_pack"]
    namespace 'app.kobuggi.hyuabot'
    compileSdk 33
    signingConfigs {
        defaultKey {
            keyAlias properties['SIGNING_KEY_ALIAS']
            keyPassword properties['SIGNING_KEY_PASSWORD']
            storeFile file(properties['SIGNING_KEY_FILE'])
            storePassword properties['SIGNING_KEY_STORE_PASSWORD']
        }
    }
    defaultConfig {
        applicationId "app.kobuggi.hyuabot"
        minSdk 26
        targetSdk 33
        versionCode 333000300
        versionName "3.0"
        buildConfigField "String", "ADMOB_ID", properties["ADMOB_APP_ID"]
        buildConfigField "String", "ADMOB_NATIVE_ID", properties["ADMOB_NATIVE_ID"]
        manifestPlaceholders = [mapKey: "${properties.getProperty("MAP_API_KEY")}"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        viewBinding true
    }
    dataBinding {
        enabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "API_URL", properties["RELEASE_API_URL"]
            ndk {
                debugSymbolLevel 'FULL'
            }
            signingConfig signingConfigs.defaultKey
        }
        debug {
            buildConfigField "String", "API_URL", properties["DEBUG_API_URL"]
            signingConfig signingConfigs.defaultKey
        }
    }
    bundle {
        language {
            enableSplit false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    // Navigation component
    def nav_version = "2.5.3"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    // Hilt
    def hilt_version = "2.44"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    // Gson
    implementation 'com.google.code.gson:gson:2.10'
    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    // OkHttp
    def okHttp_version = "5.0.0-alpha.10"
    implementation "com.squareup.okhttp3:okhttp:$okHttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttp_version"
    // Coroutine
    def coroutine_version = "1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    // RxKotlin
    def rxkotlin_version = "3.0.1"
    implementation "io.reactivex.rxjava3:rxkotlin:$rxkotlin_version"
    // RxAndroid
    def rx_android_version = "3.0.2"
    implementation "io.reactivex.rxjava3:rxandroid:$rx_android_version"
    // RxJava
    def rxjava_version = "3.1.5"
    implementation "io.reactivex.rxjava3:rxjava:$rxjava_version"
    // Datastore
    def datastore_version = "1.0.0"
    implementation "androidx.datastore:datastore-preferences:$datastore_version"
    implementation "androidx.datastore:datastore-preferences-rxjava3:$datastore_version"
    // Map
    implementation 'com.google.maps.android:android-maps-utils:2.3.0'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    // ViewPager2
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    // Play Asset Delivery
    implementation 'com.google.android.play:asset-delivery:2.0.1'
    implementation 'com.google.android.play:asset-delivery-ktx:2.0.1'
    // Room
    def room_version = "2.5.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-paging:$room_version"
    // Calendar
    implementation 'com.kizitonwose.calendar:view:2.1.1'
    // Firebase
    implementation platform('com.google.firebase:firebase-bom:31.1.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx:23.1.1'
    implementation 'com.google.firebase:firebase-analytics'
}

kapt {
    correctErrorTypes = true
}