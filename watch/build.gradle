plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

android {
    namespace 'app.kobuggi.hyuabot'
    compileSdk 33
    signingConfigs {
        defaultKey {
            keyAlias properties['SIGNING_KEY_ALIAS']
            keyPassword properties['SIGNING_KEY_PASSWORD']
            storeFile file(properties['SIGNING_KEY_FILE'])
            storePassword properties['SIGNING_KEY_STORE_PASSWORD']
        }
    }
    defaultConfig {
        applicationId "app.kobuggi.hyuabot"
        minSdk 28
        targetSdk 33
        versionCode 331000103
        versionName "Wear 1.0"
        signingConfig signingConfigs.defaultKey
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "API_URL", properties["RELEASE_API_URL"]
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        debug {
            buildConfigField "String", "API_URL", properties["DEBUG_API_URL"]
        }
    }
    buildFeatures {
        viewBinding true
    }
    bundle {
        language {
            enableSplit = false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    dependenciesInfo {
        includeInApk true
        includeInBundle true
    }
    buildToolsVersion '33.0.2'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'com.google.android.gms:play-services-wearable:18.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.wear:wear:1.2.0'
    // Gson
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.9.0'
    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    // Hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"
    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.7'
    // View pager
    implementation "androidx.viewpager2:viewpager2:1.1.0-beta01"
    // RxKotlin
    def rxkotlin_version = "3.0.1"
    implementation "io.reactivex.rxjava3:rxkotlin:$rxkotlin_version"
    // RxAndroid
    def rx_android_version = "3.0.2"
    implementation "io.reactivex.rxjava3:rxandroid:$rx_android_version"
    // RxJava
    def rxjava_version = "3.1.5"
    implementation "io.reactivex.rxjava3:rxjava:$rxjava_version"
}

kapt {
    correctErrorTypes true
}